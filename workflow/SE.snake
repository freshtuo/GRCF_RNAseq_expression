configfile: "config/config.yaml"

def get_read1_file(wildcards):
    return config["read1"][wildcards.sample]

rule all:
    input:
        rawqc=expand("results/raw_fastq_qc/{sample}_R1_fastqc.html", sample=config["read1"]),
        trimqc=expand("results/trim_fastq_qc/{sample}_R1.trimmed_fastqc.html", sample=config["read1"]),
        bam=expand("results/star_align/{sample}/{sample}.Aligned.sortedByCoord.out.bam", sample=config["read1"]),
        bamqc=expand("results/bam_qc/{sample}.rRNA_stat.txt", sample=config["read1"]),
        fpkm=expand("results/cufflinks_fpkm/{sample}/genes.fpkm_tracking", sample=config["read1"]),
        counts=expand("results/htseq_count_reads/{sample}.genes.HTSeq.count", sample=config["read1"]),
        mutiqc="results/multiqc/multiqc_report.html",
        fpkmsum="results/summarize_fpkm/expression.genes.xlsx",
        countssum="results/summarize_counts/raw_counts_genes.txt.gz"

rule rename_fastq:
    input:
        read1=get_read1_file
    output:
        read1=temp("results/rename_fastq/{sample}_R1.fastq.gz")
    shell:
        "cp {input.read1} {output.read1}"

rule raw_fastq_qc:
    input:
        read1="results/rename_fastq/{sample}_R1.fastq.gz"
    output:
        out1="results/raw_fastq_qc/{sample}_R1_fastqc.html"
    log:
        "results/logs/raw_fastq_qc/{sample}.log"
    conda:
        "../envs/fastqc.yaml"
    threads: 4
    shell:
        "fastqc --outdir results/raw_fastq_qc --threads {threads} --quiet {input.read1} >{log} 2>&1"

rule trim_adapter:
    input:
        read1="results/rename_fastq/{sample}_R1.fastq.gz"
    output:
        read1=temp("results/trim_fastq/{sample}_R1.trimmed.fastq.gz")
    params:
        overlap=config["overlap"],
        adapter3=config["adapter3"],
        adapter5=config["adapter5"],
        minlen=config["minlen"],
        qscore=config["qscore"]
    log:
        "results/logs/trim_fastq/{sample}.log",
    threads: 4
    conda:
        "../envs/cutadapt.yaml"
    shell:
        "cutadapt -j {threads} -O {params.overlap} -m {params.minlen} -q {params.qscore} -a {params.adapter3} -o {output.read1} {input.read1} >{log} 2>&1; "

rule trim_fastq_qc:
    input:
        read1="results/trim_fastq/{sample}_R1.trimmed.fastq.gz"
    output:
        out1="results/trim_fastq_qc/{sample}_R1.trimmed_fastqc.html"
    log:
        "results/logs/trim_fastq_qc/{sample}.log"
    conda:
        "../envs/fastqc.yaml"
    threads: 4
    shell:
        "fastqc --outdir results/trim_fastq_qc --threads {threads} --quiet {input.read1} >{log} 2>&1"

rule star_align:
    input:
        read1="results/trim_fastq/{sample}_R1.trimmed.fastq.gz"
    output:
        bam="results/star_align/{sample}/{sample}.Aligned.sortedByCoord.out.bam",
        bai="results/star_align/{sample}/{sample}.Aligned.sortedByCoord.out.bam.bai"
    log:
        bam="results/logs/star_align/{sample}.star.log",
        bai="results/logs/star_align/{sample}.samtools.log"
    conda:
        "../envs/star.yaml"
    threads: 8
    script:
        "../scripts/star_align.py"

rule bam_qc:
    input:
        bam="results/star_align/{sample}/{sample}.Aligned.sortedByCoord.out.bam"
    output:
        stat="results/bam_qc/{sample}.bam_stats.txt",
        gcov="results/bam_qc/{sample}.geneBodyCoverage.curves.png",
        rdis="results/bam_qc/{sample}.read_distribution.txt",
        rrna="results/bam_qc/{sample}.rRNA_stat.txt"
    params:
        hkgs=config["hkgs"],
        rsgs=config["rsgs"],
        rrnas=config["rrnas"]
    log:
        stat="results/logs/bam_qc/{sample}.bam_stat.log",
        gcov="results/logs/bam_qc/{sample}.geneBody_coverage.log",
        rdis="results/logs/bam_qc/{sample}.read_distribution.log",
        rrna="results/logs/bam_qc/{sample}.rRNA_stat.log"
    conda:
        "../envs/rseqc.yaml"
    shell:
        "bam_stat.py -i {input.bam} >{output.stat} 2>{log.stat}; "
        "geneBody_coverage.py -r {params.hkgs} -i {input.bam} -o results/bam_qc/{wildcards.sample} -f png >{log.gcov} 2>&1; "
        "read_distribution.py -i {input.bam} -r {params.rsgs} >{output.rdis} 2>{log.rdis}; "
        "split_bam.py -i {input.bam} -r {params.rrnas} -o results/bam_qc/{wildcards.sample}.rRNA >{output.rrna} 2>{log.rrna}; "
        "rm results/bam_qc/{wildcards.sample}.rRNA*.bam; "

rule cufflinks_fpkm:
    input:
        bam="results/star_align/{sample}/{sample}.Aligned.sortedByCoord.out.bam"
    output:
        genes="results/cufflinks_fpkm/{sample}/genes.fpkm_tracking",
        isoforms="results/cufflinks_fpkm/{sample}/isoforms.fpkm_tracking"
    params:
        refgtf=config["refgtf"],
        refseq=config["refseq"],
        maskgtf=config["maskgtf"],
        maxBFrag=config["maxBFrag"],
        libtype=config["libtype"]
    log:
        "results/logs/cufflinks_fpkm/{sample}.cufflinks.log"
    conda:
        "../envs/cufflinks.yaml"
    threads: 8
    shell:
        "cufflinks --output-dir results/cufflinks_fpkm/{wildcards.sample} --num-threads {threads} "
        "--GTF {params.refgtf} --compatible-hits-norm  --max-bundle-frags {params.maxBFrag} --upper-quartile-norm "
        "--frag-bias-correct {params.refseq} --multi-read-correct --library-type {params.libtype} --mask-file {params.maskgtf} "
        "{input.bam} >{log} 2>&1; "
        "gzip {log}"

rule htseq_count_reads:
    input:
        bam="results/star_align/{sample}/{sample}.Aligned.sortedByCoord.out.bam"
    output:
        counts="results/htseq_count_reads/{sample}.genes.HTSeq.count"
    params:
        libtype=config["libtype"],
        minaqual=config["minaqual"],
        refgtf=config["refgtf"],
        mem=config["mem"]
    log:
        htseq="results/logs/htseq_count_reads/{sample}.HTSeq.log.gz"
    conda:
        "../envs/htseq.yaml"
    threads: 8
    script:
        "../scripts/htseq_count_reads.py"

rule multiqc:
    input:
        rawfqqc=expand("results/raw_fastq_qc/{sample}_R1_fastqc.html", sample=config["read1"]),
        staralign=expand("results/star_align/{sample}/{sample}.Aligned.sortedByCoord.out.bam", sample=config["read1"]),
        bamqc=expand("results/bam_qc/{sample}.read_distribution.txt", sample=config["read1"]),
        htcount=expand("results/htseq_count_reads/{sample}.genes.HTSeq.count", sample=config["read1"])
    output:
        html="results/multiqc/multiqc_report.html"
    log:
        "results/logs/multiqc/multiqc.log"
    conda:
        "../envs/multiqc.yaml"
    shell:
        """multiqc results/raw_fastq_qc/ results/star_align/ results/bam_qc/ results/htseq_count_reads/ """
        """--ignore "*_STARpass1" """
        """-o results/multiqc >{log} 2>&1"""

rule summarize_fpkm:
    input:
        gene=expand("results/cufflinks_fpkm/{sample}/genes.fpkm_tracking", sample=config["read1"]),
        isoform=expand("results/cufflinks_fpkm/{sample}/isoforms.fpkm_tracking", sample=config["read1"])
    output:
        gene="results/summarize_fpkm/expression.genes.xlsx",
        isoform="results/summarize_fpkm/expression.isoforms.xlsx",
        box="results/summarize_fpkm/fpkm.genes.boxplot.png"
    log:
        "results/logs/summarize_fpkm/summarize_fpkm.log"
    conda:
        "../envs/python.yaml"
    script:
        "../scripts/summarize_fpkm.py"

rule summarize_counts:
    input:
        counts=expand("results/htseq_count_reads/{sample}.genes.HTSeq.count", sample=config["read1"])
    output:
        counts="results/summarize_counts/raw_counts_genes.txt.gz",
        other="results/summarize_counts/raw_counts_other.txt.gz"
    log:
        "results/logs/summarize_counts/summarize_counts.log"
    conda:
        "../envs/python.yaml"
    script:
        "../scripts/summarize_counts.py"

